plugins {
  id 'org.ajoberstar.grgit' version '1.4.2'
}

apply plugin: 'java'
apply plugin: 'eclipse'

import org.ajoberstar.grgit.*
import org.apache.tools.ant.filters.ReplaceTokens

version = generateVersion('1.0')
String pluginMain = 'com.lesserhydra.magicskulls.MagicSkulls'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
  }
}

dependencies {
  compile 'org.jetbrains:annotations:15.0'
  compile 'org.bukkit:bukkit:1.11-R0.1-SNAPSHOT'
}

//Generate plugin.yml
processResources {
  inputs.file 'build.gradle' //So it's regenerated if version changes
  from 'templates/plugin.yml'
  filter(ReplaceTokens, tokens: [
          version:		version,
          mainClass:  pluginMain,
  ])
}

static String generateVersion(workingVersion) {
  Grgit repo = Grgit.open()
  Tag currentTag = repo.tag.list().find {it.commit == repo.head()}

  if (currentTag == null) return workingVersion + '-SNAPSHOT'
  return currentTag.getName()
}
